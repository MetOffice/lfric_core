!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------
!> @brief Algorithm for 3D COSMIC transport scheme.
!> @details The algorithm performs a horizontal update of density using Cosmic
!>          and then does a single Cosmic update in the vertical. The algorithm
!>          may be defined as rho_np1 = Z(rho_xy) with rho_xy the horizontal
!>          transport of rho.
module cosmic_threed_alg_mod

  use constants_mod,                     only: i_def
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use finite_element_config_mod,         only: element_order
  use fs_continuity_mod,                 only: W2, W3
  use runtime_constants_mod,             only: get_coordinates,               &
                                               get_cell_orientation,          &
                                               get_detj_at_w2
  use cusph_split_transport_alg_mod,     only: cusph_split_transport_alg
  use cusph_fv_density_update_alg_mod,   only: cusph_fv_density_update_alg
  use flux_direction_mod,                only: x_direction, y_direction,      &
                                               z_direction
  use vert_conservative_cosmic_alg_mod,  only: vert_conservative_cosmic_alg
  use cosmic_density_update_alg_mod,     only: cosmic_density_update_alg
  use output_config_mod,                 only: subroutine_timers
  use timer_mod,                         only: timer
  use timestepping_config_mod,           only: dt

  implicit none

  private

  public :: cosmic_threed_transport_step

contains

  !> @brief Algorithm for 3D COSMIC transport scheme.
  !> @details The algorithm performs a horizontal update of density using Cosmic
  !>          and then does a single Cosmic update in the vertical. The algorithm
  !>          may be defined as rho_np1 = Z(rho_xy) with rho_xy the horizontal
  !>          transport of rho.
  !> @param[in,out] rho          Density field
  !> @param[in]     dep_pts_x    Departure points in x direction
  !> @param[in]     dep_pts_y    Departure points in y direction
  !> @param[in]     dep_pts_z    Departure points in z direction
  subroutine cosmic_threed_transport_step( rho,           &
                                           dep_pts_x,     &
                                           dep_pts_y,     &
                                           dep_pts_z )

    implicit none

    type(field_type), intent(inout) :: rho
    type(field_type), intent(in)    :: dep_pts_x
    type(field_type), intent(in)    :: dep_pts_y
    type(field_type), intent(in)    :: dep_pts_z

    type(field_type), pointer :: chi(:) => null()
    type(field_type), pointer :: cell_orientation => null()
    type(field_type), pointer :: detj_at_w2 => null()

    type(field_type) :: mass_flux_x, mass_flux_y, mass_flux_z
    type(field_type) :: rho_n, rho_x, rho_xy

    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    chi              => get_coordinates()
    cell_orientation => get_cell_orientation()
    detj_at_w2       => get_detj_at_w2()

    w2_fs => dep_pts_x%get_function_space()
    w3_fs => rho%get_function_space()

    rho_n  = field_type( vector_space = w3_fs )
    rho_x  = field_type( vector_space = w3_fs )
    rho_xy = field_type( vector_space = w3_fs )

    mass_flux_x = field_type( vector_space = w2_fs )
    mass_flux_y = field_type( vector_space = w2_fs )
    mass_flux_z = field_type( vector_space = w2_fs )

    call invoke( setval_X( rho_n, rho ) )

    if ( subroutine_timers ) call timer('3D Cosmic update')

    ! Calculate mass fluxes in horizontal direction using Cosmic.
    call cusph_split_transport_alg( rho_n, cell_orientation,         &
                                    mass_flux_x, mass_flux_y,        &
                                    dep_pts_x, dep_pts_y, detj_at_w2 )

    ! Update the density field given horizontal mass fluxes.
    call cusph_fv_density_update_alg( rho_n, mass_flux_x, mass_flux_y, rho_x,   &
                                      cell_orientation, x_direction, detj_at_w2 )
    call cusph_fv_density_update_alg( rho_x, mass_flux_x, mass_flux_y, rho_xy,  &
                                      cell_orientation, y_direction, detj_at_w2 )

    ! Update density in the vertical given the horizontally transported density.
    call vert_conservative_cosmic_alg( mass_flux_z, rho_xy, dep_pts_z, dt )

    call cosmic_density_update_alg( rho, rho_xy, mass_flux_z, detj_at_w2,       &
                                    cell_orientation, z_direction )

    if ( subroutine_timers ) call timer('3D Cosmic update')

    nullify( chi, cell_orientation, detj_at_w2, w2_fs, w3_fs )

  end subroutine cosmic_threed_transport_step

end module cosmic_threed_alg_mod
